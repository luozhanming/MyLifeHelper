if (isGithubApp) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.apollographql.android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion complie_sdk_version

    defaultConfig {
        //library不能有applicationId
        if (isGithubApp) {
            applicationId "cn.luozhanming.github"
        }
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        String token = properties.getProperty("ACCESS_TOKEN")
        String clientId = properties.getProperty("CLIENT_ID")
        String clientSecret = properties.getProperty("CLIENT_SECRET")
        buildConfigField "String", "ACCESS_TOKEN", "\"$token\""
        buildConfigField "String","CLIENT_ID","\"$clientId\""
        buildConfigField "String","CLIENT_SECRET","\"$clientSecret\""


        multiDexEnabled true


    }

    apollo {
        generateKotlinModels = true // or false
    }

    sourceSets {
        main {
            if (isGithubApp) {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/runalone/java']
                res.srcDirs = ['src/main/res', 'src/main/runalone/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':library')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation deps.apollo
    implementation deps.apollo_rxjava2
    kapt deps.dagger_compiler
    kapt deps.dagger_android_compiler
}
